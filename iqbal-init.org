* Declare global variables and utility functions
  Set global variables to be used in the rest of the intialization
** 1. The full path to emacs.d
   This is needed sometimes (eg. for ternjs on windows)
   #+begin_src emacs-lisp
     (defvar iqbal-home-dir (concat (expand-file-name "~/.emacs.d/")))
   #+end_src
   
** 2. Path to 'non-el-get' packages
  Packages not currently installable via el-get reside in this directory
  #+begin_src emacs-lisp
    (defvar otherpackages "~/.emacs.d/otherpackages/")
  #+end_src

** 3. Path to 'lang'uage specific configurations
   Lang contain the configuration related to one specific type of file.
   They reside in the following directory
   #+begin_src emacs-lisp
     (defvar iqbal-langs-dir "~/.emacs.d/lang/")
   #+end_src

** 4. Check if given file is older than any of the other given files
   #+begin_src emacs-lisp
     (defun iqbal-is-file-stale (orig-file &rest files)
       (reduce (lambda (is-stale-yet? file)
                 (or is-stale-yet?
                     (file-newer-than-file-p file orig-file)))
               files
               :initial-value nil))
   #+end_src

** 5. Helper functions to read/write from/to file
   #+begin_src emacs-lisp
     (defun iqbal-read-file (path)
       (with-temp-buffer
         (insert-file-contents-literally path)
         (buffer-substring-no-properties (point-min) (point-max))))
     
     (defun iqbal-write-to-file (file data)
       (with-temp-file file
         (erase-buffer)
         (insert data)))
   #+end_src

** 6. Combine the code in the given org-files to a single string
   #+begin_src emacs-lisp
     (defun iqbal-combine-org-files (target sources language)
       (let* ((extracted-files (mapcar (lambda (source)
                                         (let ((export-file (concat (file-name-sans-extension source) ".el")))
                                           (org-babel-tangle-file source 
                                                                  export-file
                                                                  language)
                                           export-file))
                                       sources))
              (extracted-code (reduce (lambda (code input-file)
                                        (if input-file
                                            (concat code (iqbal-read-file input-file))
                                          code))
                                      extracted-files :initial-value "")))
         (when target
           (iqbal-write-to-file target extracted-code))
         extracted-code))
   #+end_src

** 7. Function to load language configuration
   A simple helper function to load a particular language configuration.
   Instead of loading the org files one by one it combines all the code in
   them in one compiled file and loads that file, the compiled file is regenerated
   if any of the org files change
   #+begin_src emacs-lisp
     (defun iqbal-load-lang (language)
       (interactive 
        (list (ido-completing-read "Language: " 
                                   (delete "."
                                           (delete ".." 
                                                   (directory-files iqbal-langs-dir))))))
       (let* ((lang-path (concat iqbal-langs-dir language "/"))
              (compiled-file (concat lang-path "/.compiled.el"))
              (source-files (remove-if-not #'file-exists-p
                                           (mapcar (lambda (file)
                                                     (concat lang-path file))
                                                   '("install.org" "setup.org" "keybindings.org")))))
         (when (or (not (file-exists-p compiled-file))
                   (apply #'iqbal-is-file-stale compiled-file source-files))
           (iqbal-combine-org-files compiled-file source-files "emacs-lisp")
           (byte-compile-file compiled-file))
         (load-file compiled-file)))
     
     (defun iqbal-install-lang (language)
       (interactive 
        (list (ido-completing-read "Language: " 
                                   (delete "."
                                           (delete ".." 
                                                   (directory-files iqbal-langs-dir))))))
       (let ((install-file (concat iqbal-langs-dir 
                                language "/install.org")))
             (if (file-exists-p install-file)
                 (org-babel-load-file install-file))))
   #+end_src

** 8. Function to declare one-shot keybindings
    These bindings active only after a particular command and are
    deactivated as soon as some other key is pressed (as in 'C-x e'
    and 'e'). Borrowed from [[https://github.com/magnars/.emacs.d][Magnar Sveen's .emacs.d]]
    #+begin_src emacs-lisp
      (defun one-shot-keybinding (key command)
        (set-temporary-overlay-map
         (let ((map (make-sparse-keymap)))
           (define-key map (kbd key) command)
           map) t))
    #+end_src

** 9. Interacting with REPL
   #+begin_src emacs-lisp
     (defmacro iqbal-evaluate-line-in-repl (name send-string-func)
       `(defun ,name ()
          (interactive)
          (,send-string-func (buffer-substring (line-beginning-position)
                                                    (line-end-position)))))
     
     (defmacro iqbal-evaluate-file-in-repl (name send-string-func)
       `(defun ,name (file)
          (interactive (list (ido-read-file-name "File to evaluate: ")))
          (,send-string-func (iqbal-read-file file))))
     
     (defmacro iqbal-evaluate-buffer-in-repl (name send-string-func)
       `(defun ,name ()
          (interactive)
          (,send-string-func (buffer-string))))
     
     (defmacro iqbal-evaluate-defun-in-repl (name send-string-func)
       `(defun ,name ()
          (interactive)
          (let ((start (save-excursion (beginning-of-defun) (point)))
                (end   (save-excursion (end-of-defun) (point))))
            (,send-string-func (buffer-substring start
                                                 end)))))
     
   #+end_src

** 10. Highlighting the line to reorient the user
   #+begin_src emacs-lisp
     (defmacro iqbal-highlight-line-after-func (func)
       `(progn (defadvice ,func (after highlight-current-line
                                            (&rest args))
                 "Flash the current line after tern jump"
                 (sit-for 0.1)
                 (iqbal-highlight-line))
               (ad-activate ',func)))
   #+end_src


* Unbind keys
** Unset C-z, I don't find it useful
   #+begin_src emacs-lisp
     (global-unset-key "\C-z")
   #+end_src

** Unset C-x C-c to avoid killing emacs mistakenly
   #+begin_src emacs-lisp
     (global-unset-key (kbd "C-x C-c"))
     (global-set-key (kbd "C-x r q") 'save-buffers-kill-emacs)
   #+end_src

   

* Enable some disable commands
  #+begin_src emacs-lisp
    (put 'narrow-to-region 'disabled nil)
    (put 'scroll-left 'disabled nil)
  #+end_src


* Integration with system clipboard
** Use system clipboard, these are helpful only when running GUI emacs
  #+begin_src emacs-lisp
    (setq x-select-enable-clipboard t)
    (setq save-interprogram-paste-before-kill t)
  #+end_src

** Use 'xsel' for kill and yank on emacs run in terminal
   #+begin_src emacs-lisp
     (unless window-system
       (when (getenv "DISPLAY")
         (if (executable-find "xsel")
             (progn
               ;; Callback for when user cuts
               (defun xsel-cut-function (text &optional push)
                 ;; Insert text to temp-buffer, and "send" content to xsel stdin
                 (with-temp-buffer
                   (insert text)
                   ;; I prefer using the "clipboard" selection (the one the
                   ;; typically is used by c-c/c-v) before the primary selection
                   ;; (that uses mouse-select/middle-button-click)
                   (call-process-region (point-min) (point-max) "xsel" nil 0 nil "--clipboard" "--input")))
               ;; Call back for when user pastes
               (defun xsel-paste-function()
                 ;; Find out what is current selection by xsel. If it is different
                 ;; from the top of the kill-ring (car kill-ring), then return
                 ;; it. Else, nil is returned, so whatever is in the top of the
                 ;; kill-ring will be used.
                 (let ((xsel-output (shell-command-to-string "xsel --clipboard --output")))
                   (unless (string= (car kill-ring) xsel-output)
                     xsel-output )))
               ;; Attach callbacks to hooks
               (setq interprogram-cut-function 'xsel-cut-function)
               (setq interprogram-paste-function 'xsel-paste-function))
           (message "Install `xsel' for integrating copy-paste between emacs run in terminal and other programs"))))
   #+end_src


* Replace yes-no questions with y-n questions
  #+begin_src emacs-lisp
    (fset 'yes-or-no-p 'y-or-n-p)
  #+end_src
  

* Create auto-save directory if it does not already exist
  #+begin_src emacs-lisp
    (unless (file-exists-p "~/.emacs.d/auto-save/")
      (make-directory "~/.emacs.d/auto-save/"))
  #+end_src

  
* Declare common keybindings
  These don't actually bind any command rather they define the keys that will 
  be used for common actions across multiple modes for commands 
  like jumping-to-definition etc. These keys will be bound to actual 
  functions by the respective major modes.

** Jumping to definitions
   #+begin_src emacs-lisp
     (defvar iqbal-jump-to-definition (kbd "M-."))
     (defvar iqbal-pop-jump-to-definition-marker (kbd "M-,"))
   #+end_src
   
** Displaying doc
   #+begin_src emacs-lisp
     (defvar iqbal-show-doc (kbd "C-c d"))
   #+end_src

** Refactoring
   #+begin_src emacs-lisp     
     (defvar iqbal-refactor-rename (kbd "C-c r"))
     (defvar iqbal-refactor-auto-import (kbd "C-c i"))
     (defvar iqbal-refactor-organize-imports (kbd "C-c o"))
   #+end_src

** Interacting with REPL
   #+begin_src emacs-lisp
     (defvar iqbal-run-shell (kbd "C-c C-z"))
     (defvar iqbal-send-region (kbd "C-c C-r"))
     (defvar iqbal-send-buffer (kbd "C-c C-b"))
     (defvar iqbal-send-line (kbd "C-c C-l"))
     (defvar iqbal-send-file (kbd "C-c C-f"))
     (defvar iqbal-send-function (kbd "C-M-x"))
     (defvar iqbal-send-phrase/sexp/block (kbd "C-c C-e"))
   #+end_src

   
* Bootstrap el-get 
  Install El-Get is not installed and configure it
** Initialize El-Get
   #+begin_src emacs-lisp
     (add-to-list 'load-path "~/.emacs.d/el-get/el-get")
     
     (unless (require 'el-get nil 'noerror)
       (with-current-buffer
           (url-retrieve-synchronously
            "https://raw.github.com/dimitri/el-get/master/el-get-install.el")
         (let (el-get-master-branch)
           (goto-char (point-max))
           (eval-print-last-sexp))))
   #+end_src
   
** Path to El-Get recipies
   Use recipies from this directory
   #+begin_src emacs-lisp
       (add-to-list 'el-get-recipe-path "~/.emacs.d/recipies/")
   #+end_src

** Start El-Get
   
   #+begin_src emacs-lisp
     (el-get 'sync)
   #+end_src
   

* Initialize package management
  Initialize package manager and add repositories
  #+begin_src emacs-lisp
      (add-to-list 'package-archives
                 '("melpa" . "http://melpa.milkbox.net/packages/") t)
      (add-to-list 'package-archives
                 '("marmalade" . "http://marmalade-repo.org/packages/") t)
      (add-to-list 'package-archives
			     '("geiser" . "http://download.savannah.gnu.org/releases/geiser/packages"))
  #+end_src


* Configurations for Emacs lisp
  Loading emacs-lisp configurations here since loading it via org-babel-load-file
  can lead to circular loading. org-babel-load-file internally used find-file for
  its operations. This problematic in our case since we load the mode specific configs using
  a major mode hook, so when org-babel-load-file loads a lisp file, it triggers the
  loading of, well, the config files for emacs-lisp, which will again load the emacs-lisp
  file and so on.
  #+begin_src emacs-lisp
    (defvar iqbal-elisp-packages
      '(el-spice))
    
    (el-get 'sync iqbal-elisp-packages)
    
    (add-hook 'emacs-lisp-mode-hook 'el-spice-mode)
    (add-hook 'lisp-interaction-mode-hook 'el-spice-mode)
  #+end_src
  

* Configure loading of the major modes
** Python
  #+begin_src emacs-lisp
    (add-hook 'python-mode-hook (lambda ()
                                  (iqbal-load-lang "python")))
  #+end_src

** Javascript
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.js\\'" . (lambda ()
                                                    (iqbal-load-lang "javascript"))))
   #+end_src

** HTML mode
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.html\\'" . (lambda ()
                                                      (iqbal-load-lang "html"))))
   #+end_src  

** CSS mode
  #+begin_src emacs-lisp
    (add-hook 'css-mode-hook (lambda ()
                                  (iqbal-load-lang "css")))
  #+end_src
  
** Scheme mode
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.rkt\\'" . scheme-mode))
     
     (add-hook 'scheme-mode-hook (lambda ()
                                (iqbal-load-lang "scheme")))
   #+end_src

** Common-lisp mode
   #+begin_src emacs-lisp
     (add-hook 'lisp-mode-hook (lambda ()
                                (iqbal-load-lang "common-lisp")))
   #+end_src
   
** SML mode
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.\\(sml\\|sig\\)\\'" . (lambda ()
                                                                 (iqbal-load-lang "sml"))))
   #+end_src

** OCaml mode
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.ml[iylp]?" . (lambda ()
                                                        (iqbal-load-lang "ocaml"))))
   #+end_src

** Better mode for working with JSON
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.json\\'" . (lambda ()
                                                      (iqbal-load-lang "json"))))
   #+end_src

** Markdown mode
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.markdown\\'" . (lambda ()
                                                          (iqbal-load-lang "markdown"))))
     (add-to-list 'auto-mode-alist '("\\.md\\'" . (lambda ()
                                                    (iqbal-load-lang "markdown"))))
   #+end_src

** Apache
   #+begin_src emacs-lisp
     (defun iqbal-load-apache-conf ()
       (iqbal-load-lang "apache"))
     (add-to-list 'auto-mode-alist '("\\.htaccess\\'"   . iqbal-load-apache-conf))
     (add-to-list 'auto-mode-alist '("httpd\\.conf\\'"  . iqbal-load-apache-conf))
     (add-to-list 'auto-mode-alist '("srm\\.conf\\'"    . iqbal-load-apache-conf))
     (add-to-list 'auto-mode-alist '("access\\.conf\\'" . iqbal-load-apache-conf))
     (add-to-list 'auto-mode-alist '("sites-\\(available\\|enabled\\)/" . iqbal-load-apache-conf))     
   #+end_src

** Scala
   #+begin_src emacs-lisp
     (add-to-list 'auto-mode-alist '("\\.\\(scala\\|sbt\\)\\'" . (lambda ()
                                                      (iqbal-load-lang "scala"))))
   #+end_src

** Ruby
  #+begin_src emacs-lisp
    (add-hook 'ruby-mode-hook (lambda ()
                                  (iqbal-load-lang "ruby")))
  #+end_src


* Load common libraries
   These are general purpose libraries that can are used
   by different modes

   The libaries are loaded by the file 'modules/modules-init.org'
   #+begin_src emacs-lisp
     (org-babel-load-file "~/.emacs.d/modules/init-modules.org")
   #+end_src
   
