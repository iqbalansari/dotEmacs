* Prompt user to create directory when visiting a non-existent directory
  #+begin_src emacs-lisp
    (defun my-create-non-existent-directory ()
      (let ((parent-directory (file-name-directory buffer-file-name)))
        (when (and (not (file-exists-p parent-directory))
                   (y-or-n-p (format "Directory `%s' does not exist! Create it?" parent-directory)))
          (make-directory parent-directory t))))

    (add-to-list 'find-file-not-found-functions #'my-create-non-existent-directory)
  #+end_src


* Open a file using sudo
  These were taken from [[https://github.com/fniessen/emacs-leuven][emacs-leuven]]
  #+begin_src emacs-lisp
    (defun my-find-file-sudo-header-warning ()
      "*Display a warning in header line of the current buffer."
      (let* ((warning "WARNING: EDITING FILE WITH ROOT PRIVILEGES!")
             (space (+ 6 (- (frame-width) (length warning))))
             (bracket (make-string (/ space 2) ?-))
             (warning (concat bracket warning bracket)))
        (setq header-line-format
              (propertize warning 'face 'header-line))))

    (defun my-find-file-sudo (filename)
      "Open FILENAME with root privileges."
      (interactive (list (ido-read-file-name "File name: ")))
      (set-buffer (find-file (concat "/sudo::" filename)))
      (my-find-file-sudo-header-warning))
  #+end_src


* Open current file in sudo
  #+begin_src emacs-lisp
    (defun my-sudo-open-this-file ()
      (interactive)
      (if (buffer-file-name)
          (my-find-file-sudo (buffer-file-name))
        (message "Current buffer is not visiting a file")))
  #+end_src


* Keybinding to save current file's name to kill-ring
  #+begin_src emacs-lisp
    (defun my-copy-file-path (arg)
      "With prefix argument copy the name of current buffer
    Else if we are in dired mode copy the path to directory,
    Otherwise copy the path to the file the current buffer is
    visiting"
      (interactive "P")
      (let (kill-text what)
        (cond
         (current-prefix-arg (setq kill-text (buffer-name))
                             (setq what " buffer's name"))
         ((eq major-mode 'dired-mode) (setq kill-text (expand-file-name default-directory)) (setq what " directory's path"))
         ((buffer-file-name) (setq kill-text (buffer-file-name)) (setq what " file's path"))
         (t (setq kill-text (buffer-name))
            (setq what " buffer's name")))
        (when kill-text
          (kill-new kill-text)
          (message (concat "Copied" what)))))

    (global-set-key (kbd "C-c f") 'my-copy-file-path)
  #+end_src


* Function to rename current buffer and associated file taken from
  [[http://whattheemacsd.com/file-defuns.el-01.html][whattheemacsd]].
  #+begin_src emacs-lisp
    (defun rename-current-buffer-file ()
      "Renames current buffer and file it is visiting."
      (interactive)
      (let ((name (buffer-name))
            (filename (buffer-file-name)))
        (if (not (and filename (file-exists-p filename)))
            (error "Buffer '%s' is not visiting a file!" name)
          (let ((new-name (read-file-name "New name: " filename)))
            (if (get-buffer new-name)
                (error "A buffer named '%s' already exists!" new-name)
              (rename-file filename new-name 1)
              (rename-buffer new-name)
              (set-visited-file-name new-name)
              (set-buffer-modified-p nil)
              (message "File '%s' successfully renamed to '%s'"
                       name (file-name-nondirectory new-name)))))))

    (global-set-key (kbd "C-x C-r") 'rename-current-buffer-file)
  #+end_src


* Function to delete current buffer and its associated file
  #+begin_src emacs-lisp
    (defun delete-file-and-buffer ()
      "Kill the current buffer and deletes the file it is visiting."
      (interactive)
      (let* ((filename (buffer-file-name))
             (delete-confirmed? (y-or-n-p (format "Are you sure you want to delete %s?" filename))))
        (when (and filename delete-confirmed?)
          (if (vc-backend filename)
              (vc-delete-file filename)
            (progn
              (delete-file filename)
              (message "Deleted file %s" filename)
              (kill-buffer))))))
  #+end_src


* Do not prompt to apply local vars if user has said no once
  #+begin_src emacs-lisp
    (defvar my-local-vars-file (locate-user-emacs-file ".disabled-local-vars")
      "Directories with ignore local vars are stored here")
    
    (defvar my-ignored-local-vars (when (file-exists-p my-local-vars-file)
                                       (read (my-read-file my-local-vars-file)))
      "Local copy of directories for which local vars are ignored")
    
    (defun my-store-ignored-local-vars ()
      "Write the local copy of ignored directories to disk wiil be
    restored on startup"
      (my-write-to-file my-local-vars-file
                           (prin1-to-string my-ignored-local-vars)))
    
    (defun my-enable-local-vars-for (dir)
      "Re-enable local vars for directory"
      (interactive (list (ido-completing-read "Enable local vars for: "
                                              my-ignored-local-vars)))
      (setq my-ignored-local-vars (delete dir my-ignored-local-vars)))
    
    (defadvice hack-local-variables-confirm (around my-cache-local-vars-resp
                                                    (all-vars unsafe-vars risky-vars dir-name))
      "If the user reply in negative to applying local vars remember it and do not
    prompt the user the next time, otherwise ask the user for confirmation and
    remember the response if it is negative"
      (unless (member dir-name my-ignored-local-vars)
        ad-do-it
        (unless ad-return-value
          (add-to-list 'my-ignored-local-vars dir-name)))
      (setq ad-return-value nil))
    
    ;; Activate the advice
    (ad-activate 'hack-local-variables-confirm)
    
    ;; Store the user choices when emacs exists
    (add-hook 'kill-emacs-hook #'my-store-ignored-local-vars)
  #+end_src
