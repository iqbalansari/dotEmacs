* Function to find GNU global's version
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-gnu-global-version "0")
    (defvar iqbal-gtags-available nil)

    (defun iqbal--get-gnu-global-version ()
      (when (executable-find "global")
        (car (last (split-string (car (split-string (shell-command-to-string "global --version") "\n"))
                                 " ")))))
  #+END_SRC


* Project root for current buffer
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-project-root nil)
  #+END_SRC


* Helper functions to retrieve and format documentation for function using gtags output
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-php-format-doc-string (docstring)
       "Strip comments from php DOCSTRING"
       (with-current-buffer (get-buffer-create " *php-ggtags-doc-format*")
         (erase-buffer)
         (insert docstring)
         (goto-char (point-min))
         
         (html2text)
         (while (re-search-forward "^\\s-*\\(?:\\*/\\|/\\*\\|\\*\\|//\\)\\s-*" nil t)
           (replace-match "" nil nil))

         (delay-mode-hooks (php-mode))
         (font-lock-default-function 'php-mode)
         (goto-char (point-min))
         (let ((font-lock-keywords '(("@[a-z]+" 0 'php-annotations-annotation-face)))
               (font-lock-string-face nil))
           (font-lock-mode)
           (font-lock-default-fontify-region (point-min) (point-max) nil))
         
         (string-trim (buffer-string))))


     (defun iqbal-php-grab-comments (location)
       "Get the comments for the function starting at location"
       (let (comments)
         (goto-char location)
         (while (and (zerop (forward-line -1))
                     (string-match-p "^\\s-*\\(?:/\\*\\|\\*\\|//\\|\\*/\\).*"
                                     (buffer-substring (line-beginning-position)
                                                       (line-end-position))))
           (push (buffer-substring-no-properties (line-beginning-position) (line-end-position))
                 comments))
         (mapconcat 'identity comments "\n")))

     (defun iqbal-php-retrieve-and-format-docstring (location)
       "Retrieve the docstring for the function at LOCATION and return formatted docstring
     LOCATION is assumed to be a list having file location, symbol name, line number and function
     signature in same order"
       (with-current-buffer (get-buffer-create " *php-ggtags-doc*")
         (erase-buffer)
         (insert-file-contents-literally (car location))
         (goto-line (string-to-int (caddr location)))
         (string-trim (format "%s\n\n%s\n\nUser defined function in %s"
                              (iqbal-php-format-doc-string (iqbal-php-grab-comments (point)))
                              (iqbal-php-format-eldoc-string (apply #'mapconcat (list 'identity (cdddr location) " ")) 0)
                              (abbreviate-file-name (car location))))))
   #+END_SRC


* Helper function to format the documentation retrieved using php-extras
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-php-format-php-extras-doc (doc)
       "Format the doc returned by php-extras this fontifies the function signature in the doc"
       (with-current-buffer (get-buffer-create " *php-doc-format")
         (erase-buffer)
         (insert doc)
         (goto-char (point-min))
         (delay-mode-hooks (php-mode))
         (font-lock-default-function 'php-mode)
         (forward-paragraph)
         (forward-line)
         (font-lock-default-fontify-region (line-beginning-position)
                                           (line-end-position)
                                           nil)
         (buffer-string)))
   #+END_SRC


* Function to retrieve documentation for symbol using gtags or php-extras
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-php-get-doc (symbol)
       "Try fetching documentation for symbol using gtags otherwise fallback to php-extras"
       (let* ((doc (php-extras-function-documentation symbol))
              (location (when (and (not doc) iqbal-gtags-available)
                          (shell-command-to-string (format "global -a -d %s --result cscope"
                                                           (shell-quote-argument symbol))))))
         (if doc
             (iqbal-php-format-php-extras-doc doc)
           (when (and location
                      (not (string= location "global: GTAGS not found.\n"))
                      (not (string= location "")))
             (iqbal-php-retrieve-and-format-docstring (split-string location " "))))))
   #+END_SRC


* Function to show documentation for symbol at point
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-php-doc-at-point (symbol)
      "Show documentation for symbol at point"
      (interactive (list (symbol-at-point)))
      (if (not symbol)
          (error "No symbol at point")
        (let ((doc (iqbal-php-get-doc (symbol-name symbol))))
          (if doc
              (with-electric-help (lambda () (insert doc)) "*php-doc*")
            (error (format "No doc found for %s" (symbol-name symbol)))))))
  #+END_SRC


* Custom function to combine eldoc retrieved using ggtags and php-extra eldoc
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-php-count-args (arglist)
       ;; Count the number of arguments in given argument list
       (with-current-buffer (get-buffer-create " *php-eldoc*")
         (erase-buffer)
         (setq count 1)
         (insert arglist)
         (goto-char 0)
         ;; Count the number of (toplevel) , except for commas
         ;; not inside nested function calls or strings
         (while (search-forward "," nil t)
           (let ((context (syntax-ppss)))
             ;; We are not inside possibly other function call
             (when (and (zerop (car context))
                        ;; we are not in  a string
                        (not (cadddr context)))
               (setq count (+ 1 count)))))
         count))

     (defun iqbal-php-format-eldoc-string (doc arg-number)
       (with-current-buffer (get-buffer-create " *php-eldoc*")
         (erase-buffer)
         (insert doc)
         ;; Fontify the doc
         (delay-mode-hooks (php-mode))
         (font-lock-default-function 'php-mode)
         (font-lock-default-fontify-region (point-min) (point-max) nil)
         (goto-char 0)
         ;; Delete trailing braces
         (when (re-search-forward "\\s-*{?\\s-*$" nil t)
           (replace-match "" nil nil))

         (goto-char 0)
         ;; Highlight the arg-numberth argument
         (when (and (> arg-number 0)
                    ;; Search successive arg-number , or (
                    (search-forward-regexp ",\\|(" nil t arg-number))
           (skip-chars-forward " ")
           (add-face-text-property (point) (- (search-forward-regexp ",\\|)$") 1) '(:weight bold :inherit highlight)))
         (string-trim (buffer-string))))

     (defun iqbal-php-extras-eldoc-at-pt ()
       (when (eq php-extras-function-arguments 'not-loaded)
         (php-extras-load-eldoc))
       (when (hash-table-p php-extras-function-arguments)
         (php-extras-get-function-property (php-get-pattern) 'prototype)))

     (defun iqbal-php-eldoc-function ()
       (interactive)
       ;; Find the position of opening bracket for current expression
       (let* ((open-paren (cadr (syntax-ppss)))
              ;; If the opening paren is a ( then we are probably in a function
              ;; call
              (in-function-call-maybe (and
                                       open-paren
                                       (equal (char-after open-paren) ?\()
                                       (save-excursion
                                         (goto-char (- open-paren 1))
                                         (not (equal (get-text-property (point) 'face) 'font-lock-keyword-face)))))
              ;; Get the doc current function call
              (doc (save-excursion
                     (when in-function-call-maybe
                       (goto-char open-paren))
                     (or (iqbal-php-extras-eldoc-at-pt) (when iqbal-gtags-available
                                                       (ignore-errors (ggtags-eldoc-function))))))
              ;; If we have documentation for the function try finding the argument
              ;; at point
              (arg-number (if (and in-function-call-maybe doc)
                              (iqbal-php-count-args (buffer-substring (+ open-paren 1) (point)))
                            0)))
         (when doc
           ;; Format the doc and return
           (ignore-errors (iqbal-php-format-eldoc-string doc arg-number)))))
   #+END_SRC


* Function to refresh tags on save
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-php-regenerate-tags ()
      (when (and iqbal-project-root
                 buffer-file-name
                 (locate-dominating-file buffer-file-name "GTAGS")
                 (executable-find "global"))
        (let ((default-directory iqbal-project-root))
          (start-process "PHP tags update"
                         nil
                         "global"
                         "--single-update"
                         buffer-file-name))))
  #+END_SRC


* Function to quickly start debugging a script using geben
  Copied from [[https://blogs.oracle.com/opal/entry/quick_debugging_of_php_scripts]]
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-php-debug ()
      "Run current PHP script for debugging with geben"
      (interactive)
      (if (locate-library "geben")
          (progn (call-interactively 'geben)
                 (async-shell-command
                  (format "XDEBUG_CONFIG='idekey=emacs-geben' %s -d xdebug.remote_enable=on -d xdebug.remote_host=127.0.0.1 -d xdebug.remote_port=9000  %s"
                          (executable-find "php")
                          (buffer-file-name))))
        (message "`geben' is not installed")))
  #+END_SRC


* Some extra configurations for boris repl
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-boris-enable-extra-settings ()
      (company-mode -1)
      (auto-complete-mode +1)
      (setq php-extras-auto-complete-insert-parenthesis nil)
      (php-extras-autocomplete-setup)
      (eldoc-mode +1)
      (php-extras-eldoc-setup))

    (add-hook 'php-boris-mode-hook #'iqbal-boris-enable-extra-settings)
  #+END_SRC


* Remove ggtags around local eldoc function
  #+BEGIN_SRC emacs-lisp
    (add-hook 'ggtags-mode-hook (lambda ()
                                  (when (fboundp 'remove-function)
                                    (remove-function (local 'eldoc-documentation-function) 'ggtags-eldoc-function))))
  #+END_SRC


* Function to install variable analysis plugin for PHP Code Sniffer
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-phpcs-variable-analysis-path (concat (locate-user-emacs-file "etc/PHP_Codesniffer-VariableAnalysis/")))
    (defvar iqbal-phpcs-standard (expand-file-name (locate-user-emacs-file "etc/ruleset.xml")))

    (defun iqbal-get-codesniffer-path ()
      (let* ((phpcs (executable-find "phpcs"))
             (vendor-path (when phpcs (locate-dominating-file phpcs "vendor")))
             (codesniffer-path (concat vendor-path "vendor/squizlabs/php_codesniffer/CodeSniffer/")))
        (when (file-exists-p codesniffer-path)
          codesniffer-path)))


    (defun iqbal-install-variable-analysis-sniffs ()
      (let* ((codesniffer-path (iqbal-get-codesniffer-path))
             (variable-analysis-sniff (concat iqbal-phpcs-variable-analysis-path "Sniffs/CodeAnalysis/VariableAnalysisSniff.php"))
             (destination-sniff-file (when codesniffer-path
                                       (concat codesniffer-path
                                               "Standards/Generic/Sniffs/CodeAnalysis/VariableAnalysisSniff.php"))))
        (when codesniffer-path
          (unless (file-exists-p destination-sniff-file)
            (copy-file variable-analysis-sniff
                       destination-sniff-file))
          t)))
  #+END_SRC


* Function to format a file using 'phpcbf' command
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-php-format-this-file ()
      (interactive)
      (if (not (executable-find "phpcbf"))
          (error "`phpcbf' not found, please use composer to install php codesniffer")
        (if (not buffer-file-name)
            (error "Buffer is not currently visting any file")
          (shell-command (format "phpcbf --encoding=utf8 --tab-width=4 --standard=%s --error-severity=0 %s %s"
                                 iqbal-phpcs-standard
                                 (if (executable-find "diff") "" "--no-patch")
                                 (expand-file-name buffer-file-name)))
          (revert-buffer t t))))
  #+END_SRC


* List of projects to not prompt for gtags generation
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-php-no-tags-projects nil)
  #+END_SRC


* Do not display project name in modeline
  #+BEGIN_SRC emacs-lisp
    (setq ggtags-mode-line-project-name nil)
  #+END_SRC


* Jumping to symbol using avy
  Need a special function, since when jumping to character 'a' we want to able
  to jump to 'a' as well as '$a' (since PHP prefixes variables with '$')
  #+begin_src emacs-lisp
    (defun iqbal-avy-goto-char-php (char &optional arg)
      "Jump to the currently visible PHP identifier.
    The window scope is determined by `avy-all-windows' (ARG negates it)."
      (interactive (list (read-char "char: " t)
                         current-prefix-arg))
      (avy-with avy-goto-char
        (avy--generic-jump
         (if (= 13 char)
             "\n"
           (format "\\$?%s" (regexp-quote (string char))))
         arg
         avy-style)))
  #+end_src
