* Separate functions for moving to start/end of function and class
** A modified version of python-mode's python-nav-end-of-defun
   #+begin_src emacs-lisp
     (defun iqbal-python-nav-end-of-defun (&optional args)
       "Move point to the end of def or class.
     Returns nil if point is not in a def or class."
       (interactive)
       (let ((beg-defun-indent)
             (beg-pos (point)))
         (when (or (python-info-looking-at-beginning-of-defun)
                   (python-nav-beginning-of-defun -1)
                   (python-nav-beginning-of-defun 1))
           (setq beg-defun-indent (current-indentation))
           (while (progn
                    (python-nav-end-of-statement)
                    (python-util-forward-comment 1)
                    (and (> (current-indentation) beg-defun-indent)
                         (not (eobp)))))
           (python-util-forward-comment -1)
           (forward-line 1)
           ;; Ensure point moves forward.
           (and (> beg-pos (point)) (goto-char beg-pos)))))
   #+end_src

** Macro to define modified navigation commands
   #+begin_src emacs-lisp
     (defmacro iqbal-py-define-nav-command (name bound-regexp nav-func)
       `(defun ,name (&optional args)
          (interactive "p")
          (let ((python-nav-beginning-of-defun-regexp ,bound-regexp))
            (push-mark)
            (,nav-func args))))
   #+end_src

** Function for moving at start of class
  #+begin_src emacs-lisp
    (iqbal-py-define-nav-command iqbal-py-begining-of-class
                              "^[[:space:]]*\\_<\\(?:class\\)\\_>[[:space:]]+\\([_[:alpha:]][_[:word:]]*\\)"
                              python-nav-beginning-of-defun)
  #+end_src

** Function for moving at end of class
   #+begin_src emacs-lisp
     (iqbal-py-define-nav-command iqbal-py-end-of-class
                               "^[[:space:]]*\\_<\\(?:class\\)\\_>[[:space:]]+\\([_[:alpha:]][_[:word:]]*\\)"
                               iqbal-python-nav-end-of-defun)
   #+end_src

** Function for moving at start of function
   #+begin_src emacs-lisp
     (iqbal-py-define-nav-command iqbal-py-begining-of-defun
                               "^[[:space:]]*\\_<\\(?:def\\)\\_>[[:space:]]+\\([_[:alpha:]][_[:word:]]*\\)"
                               python-nav-beginning-of-defun)
   #+end_src

** Function for moving at end of function
   #+begin_src emacs-lisp
     (iqbal-py-define-nav-command iqbal-py-end-of-defun
                               "^[[:space:]]*\\_<\\(?:def\\)\\_>[[:space:]]+\\([_[:alpha:]][_[:word:]]*\\)"
                               iqbal-python-nav-end-of-defun)
   #+end_src


* Function to execute nose tests using the nose library 
  #+begin_src emacs-lisp
    (defun iqbal-execute-nose-tests (options nose-fn)
      (let ((debug (assoc "--pdb" options)))
        (funcall nose-fn debug)))
  #+end_src


* Setting the correct python interpreter
** Function to detect if ipython is available
   If virtualenv is active and ipython is not installed in the virtualenv
   the following will return nil, irrespective of whether ipython is installed
   globally, unless the `check-venv-p` is nil
  #+begin_src emacs-lisp
    (defun iqbal-ipython-available-in-virtualenv-p ()
      (when (executable-find "ipython")
        ;; If a virtualenv is active also make sure ipython is installed
        ;; in the virtualenv
        (or (not (bound-and-true-p pyvenv-virtual-env))
            (string-prefix-p (file-truename pyvenv-virtual-env)
                             (file-truename (executable-find "ipython"))))))
  #+end_src

** Function to setup python interpreter
   #+begin_src emacs-lisp
     (defun iqbal-set-python-interpreter ()
       ;; Do not check venv for ipython if we are deactivating the
       ;; current venv
       (setq python-shell-interpreter (if (and (iqbal-ipython-available-in-virtualenv-p)
                                               ;; Do not use ipython on windows
                                               (not (equal system-type 'windows-nt)))
                                          "ipython"
                                        "python"))

       (when (and (equal system-type 'windows-nt)
                  (not (zerop (shell-command "python -c \"import readline\""))))
         (warn "`pyreadline' not installed, completion in python shell would not work
         do `pip install pyreadline', to install pyreadline")))
   #+end_src

** Setup python interpreter before starting python shell
   #+begin_src emacs-lisp
     (advice-add 'python-shell-parse-command :before #'iqbal-set-python-interpreter)
   #+end_src
