* Open links in firefox by default
  #+BEGIN_SRC emacs-lisp
    (when (executable-find "firefox")
      (setq browse-url-browser-function #'browse-url-firefox))
  #+END_SRC


* Install emacs-w3m if w3m executable is found
   #+BEGIN_SRC emacs-lisp
     (when (executable-find "w3m")
       (iqbal-install-package 'w3m))
   #+END_SRC


* Set mark before jumping to next/previous urls
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-w3m-set-mark-before-jump ()
       (defadvice w3m-next-anchor (before iqbal-w3m-set-mark-before-next-url (&rest args))
         (push-mark))

       (defadvice w3m-previous-anchor (before iqbal-w3m-set-mark-before-previous-url (&rest args))
         (push-mark))

       (defadvice w3m-view-this-url (before iqbal-w3m-set-mark-before-viewing-url (&rest args))
         (push-mark))

       (ad-activate 'w3m-next-anchor)
       (ad-activate 'w3m-previous-anchor)
       (ad-activate 'w3m-view-this-url))
   #+END_SRC


* Keybindings for emacs-w3m
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-set-w3m-keybindings ()
       (define-key w3m-mode-map (kbd "M-n") #'w3m-next-anchor)
       (define-key w3m-mode-map (kbd "M-p") #'w3m-previous-anchor)
       (define-key w3m-mode-map (kbd "<down>") #'next-line)
       (define-key w3m-mode-map (kbd "<up>") #'previous-line)
       (define-key w3m-mode-map (kbd "n") #'next-line)
       (define-key w3m-mode-map (kbd "p") #'previous-line)
       (define-key w3m-mode-map (kbd "<left>") nil)
       (define-key w3m-mode-map (kbd "<right>") nil)
       (define-key w3m-mode-map (kbd "<C-prior>") #'w3m-previous-buffer)
       (define-key w3m-mode-map (kbd "<C-next>") #'w3m-next-buffer)
       (define-key w3m-mode-map (kbd "<C-S-prior>") #'w3m-tab-move-left)
       (define-key w3m-mode-map (kbd "<C-S-next>") #'w3m-tab-move-right)
       (define-key w3m-mode-map iqbal-hide-app #'w3m-close-window)
       (define-key w3m-mode-map (kbd "C-t") #'w3m-create-empty-session)
       (define-key w3m-mode-map (kbd "C-c C-o") #'w3m-lnum-external-view-this-url)
       (define-key w3m-mode-map (kbd "E") #'iqbal-w3m-view-url-in-external-browser)
       (define-key w3m-mode-map (kbd "M-RET") #'w3m-lnum-external-view-this-url)
       (define-key w3m-mode-map (kbd "C-w") #'w3m-delete-buffer))

     (with-eval-after-load 'w3m
       (iqbal-set-w3m-keybindings)
       (iqbal-w3m-set-mark-before-jump))
   #+END_SRC


* Delete trailing spaces in w3m buffer
   #+BEGIN_SRC emacs-lisp
     (add-hook 'w3m-display-hook (lambda (url)
                                   (let ((buffer-read-only nil))
                                     (delete-trailing-whitespace))))
   #+END_SRC


* Enable lnum mode for faster opening of urls
   #+BEGIN_SRC emacs-lisp
     (add-hook 'w3m-mode-hook 'w3m-lnum-mode)
   #+END_SRC


* Display images by default
  #+BEGIN_SRC emacs-lisp
    (setq w3m-default-display-inline-images t)
  #+END_SRC


* Jump to the title on loaded page
  A simple heuristic to avoid the useless menus on the page (which are do not
  work in w3m anyway) on a webpage and jump to the actual content
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-w3m-jump-to-title t "Jump to the title in w3m buffers after the page loads")

    (defun iqbal-w3m-goto-to-title (&optional url)
      (interactive)
      (let ((title (replace-regexp-in-string "\\(\\`[ \t\n\r]+\\|[ \t\n\r]+\\'\\)"
                                             ""
                                             (car (split-string w3m-current-title "\\( - \\| | \\)")))))
        (when (and iqbal-w3m-jump-to-title
                   (search-forward title nil t))
          (goto-char (line-beginning-position))
          (w3m-history-store-position)
          (recenter 0))))

    (add-hook 'w3m-display-hook #'iqbal-w3m-goto-to-title)
  #+END_SRC


* DWIM version of w3m-browse-url
   Prompts to open currently opened feeds URL in elfeed
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-w3m-browse-url-dwim ()
       (interactive)
       (if (and (eq major-mode 'elfeed-show-mode)
                (elfeed-entry-link elfeed-show-entry))
           (w3m-browse-url (read-string "Emacs-w3m URL: " (elfeed-entry-link elfeed-show-entry)))
         (call-interactively 'w3m-browse-url)))
   #+END_SRC


* Open current url in external browser
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-w3m-view-url-in-external-browser ()
      (interactive)
      (browse-url w3m-current-url))
  #+END_SRC


* Global w3m keybindings
  #+BEGIN_SRC emacs-lisp
    (when (executable-find "w3m")
      (define-key launcher-map "w" #'w3m)
      (global-set-key iqbal-open-link #'iqbal-w3m-browse-url-dwim))
  #+END_SRC
