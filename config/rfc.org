* Install irfc for viewing RFC files
  #+begin_src emacs-lisp
    (iqbal-install-package 'irfc)
  #+end_src


* Integration with org-mode
** Helper functions to get info about currently viewed RFC
  #+begin_src emacs-lisp
    (defun iqbal-irfc-get-title ()
      (save-excursion
        (goto-char (irfc-render-buffer-hide-blank-line (or (irfc-render-buffer-hide-whitespace-at-start)
                                                           (point-min))))
        (when (re-search-forward (concat
                                  "\\([^ \t\f\r\n].*[^ \t\f\r\n]\\)"
                                  "\\(\r?\n[ \t]*[^ \t\f\r\n].*[^ \t\f\r\n]\\)*"))
          (replace-regexp-in-string "[\r\n\t\f ]+" " " (match-string 0)))))

    (defun iqbal-irfc-get-number ()
      (save-excursion
        (goto-char (point-min))
        (when (re-search-forward "^\\(request for comments\\|updates\\|obsoletes\\):\\( RFCs\\)?[ \t]+\\(\\([0-9X]+\\)\\(,[ \t]+[0-9]+\\)*\\)"
                                 (save-excursion
                                   (irfc-render-buffer-hide-blank-line (or (irfc-render-buffer-hide-whitespace-at-start)
                                                                           (point-min))))
                                 t)
          (buffer-substring (match-beginning 3) (match-end 3)))))
  #+end_src

** Function to store an org link to given RFC
   #+begin_src emacs-lisp
     (defun iqbal-store-irfc-link ()
       (when (eq major-mode 'irfc-mode)
         (let ((rfc-number (iqbal-irfc-get-number))
               (rfc-title (iqbal-irfc-get-title)))
           (org-store-link-props :type "rfc"
                                 :link (concat "rfc:" rfc-number)
                                 :description rfc-title
                                 :rfcnumber rfc-number)
           rfc-number)))
   #+end_src

** Function to export irfc links
   #+begin_src emacs-lisp
     (defun iqbal-irfc-export (rfc-number desc format)
       "Export a irfc link from Org files."
       (let* ((web-link (format "https://tools.ietf.org/html/rfc%s" rfc-number))
              (desc (or desc web-link)))
         (cond
          ((eq format 'html) (format "<a href=\"%s\">%s</a>" web-link desc))
          ((eq format 'latex) (format "\href{%s}{%s}" web-link desc))
          ((eq format 'ascii) (format "%s (%s)" desc web-link))
          ((eq format 'md) (format "[%s](%s)" desc web-link))
          (t web-link))))
   #+end_src

** Function to open org RFC link
  #+begin_src emacs-lisp
    (defun iqbal-open-rfc-file (path)
      (irfc-visit (string-to-int path)))
  #+end_src

** Hook the above to org-mode
   #+begin_src emacs-lisp
     (defun iqbal-open-rfc-file (path)
       (irfc-visit (string-to-int path)))

     (org-add-link-type "rfc" #'iqbal-open-rfc-file #'iqbal-irfc-export)
     (add-hook 'org-store-link-functions 'iqbal-store-irfc-link)
   #+end_src
