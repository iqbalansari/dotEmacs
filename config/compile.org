* Install notify for desktop notification
  #+BEGIN_SRC emacs-lisp
    (iqbal-install-package 'notify)
  #+END_SRC


* Save files without asking before compiling 
 #+BEGIN_SRC emacs-lisp
   (setq compilation-ask-about-save nil)
 #+END_SRC


* Keybinding to compile
  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "C-c ;") #'compile)
  #+END_SRC


* Notify of completion of compilation
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-buffer-on-screen-p (buffer)
      (let ((buffer-window (get-buffer-window buffer t)))
        ;; If buffer is not open in a window it is not visible
        (and buffer-window
             ;; Check the visibility of the frame the window belongs to
             (equal (frame-visible-p (window-frame buffer-window))
                    t))))

    (defun iqbal-notify-compilation-finish (buffer message)
      (require 'subr-x)
      (with-current-buffer buffer
        ;; Display notification only if we are in compilation mode
        ;; and buffer is not visible
        (when (and (eq major-mode 'compilation-mode)
                   (not (iqbal-buffer-on-screen-p (current-buffer))))
          (let ((disp-message (format "Command: %s \n\nIn directory: %s\n\nStatus: %s"
                                      (string-trim compile-command)
                                      (string-trim default-directory)
                                      (string-trim message))))
            (notify "Emacs compile.el" disp-message)))))

    (add-hook 'compilation-finish-functions #'iqbal-notify-compilation-finish)
  #+END_SRC


* Translate ansi sequences in compilation buffers
  #+BEGIN_SRC emacs-lisp
    (autoload 'ansi-color-apply-on-region "ansi-color")

    (defun iqbal-colorize-compilation-buffer ()
      (when (eq major-mode 'compilation-mode)
        (let ((inhibit-read-only t))
          (ansi-color-apply-on-region compilation-filter-start (point)))))

    (add-hook 'compilation-filter-hook 'iqbal-colorize-compilation-buffer)
  #+END_SRC

