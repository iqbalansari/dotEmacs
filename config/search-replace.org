* Install packages useful for search and replace
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-search-replace-packages
      '(visual-regexp
        wgrep
        anzu
        swoop
        pcre2el
        isearch-dabbrev))

    (when (executable-find "ag")
      (add-to-list 'iqbal-search-replace-packages 'ag)
      (add-to-list 'iqbal-search-replace-packages 'wgrep-ag))

    (iqbal-install-packages iqbal-search-replace-packages)
  #+END_SRC


* Use findstr on windows
  Credits: [[https://github.com/npostavs/emacs.d]]
  #+BEGIN_SRC emacs-lisp
    (when (and (eq system-type 'windows-nt)
               (not (executable-find "grep")))
      (with-eval-after-load 'grep
        (grep-apply-setting 'grep-find-template
                            "findstr /S /N /D:. /C:<R> <F>")
        (setq find-name-arg nil)))
  #+END_SRC


* Use regex searches by default.
  Universal prefix argument will initiate normal search
  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "C-s") 'isearch-forward-regexp)
    (global-set-key (kbd "\C-r") 'isearch-backward-regexp)
  #+END_SRC


* Enhancements to isearch mode taken from [[https://github.com/purcell/emacs.d/blob/master/init-isearch.el][purcell/emacs.d]]
** Search for the symbol at point
   #+BEGIN_SRC emacs-lisp
     (defun isearch-yank-symbol ()
       "*Put symbol at current point into search string."
       (interactive)
       (let ((sym (symbol-at-point)))
         (if sym
             (progn
               (setq isearch-regexp t
                     isearch-string (concat "\\_<" (regexp-quote (symbol-name sym)) "\\_>")
                     isearch-message (mapconcat 'isearch-text-char-description isearch-string "")
                     isearch-yank-flag t))
           (ding)))
       (isearch-search-and-update))
   #+END_SRC

** Delete region mark and closest portion of isearch match string
   #+BEGIN_SRC emacs-lisp
     (defun zap-to-isearch (rbeg rend)
       "Kill the region between the mark and the closest portion of
     the isearch match string. The behaviour is meant to be analogous
     to zap-to-char; let's call it zap-to-isearch. The deleted region
     does not include the isearch word. This is meant to be bound only
     in isearch mode.  The point of this function is that oftentimes
     you want to delete some portion of text, one end of which happens
     to be an active isearch word. The observation to make is that if
     you use isearch a lot to move the cursor around (as you should,
     it is much more efficient than using the arrows), it happens a
     lot that you could just delete the active region between the mark
     and the point, not include the isearch word."
       (interactive "r")
       (when (not mark-active)
         (error "Mark is not active"))
       (let* ((isearch-bounds (list isearch-other-end (point)))
              (ismin (apply 'min isearch-bounds))
              (ismax (apply 'max isearch-bounds)))
         (if (< (mark) ismin)
             (kill-region (mark) ismin)
           (if (> (mark) ismax)
               (kill-region ismax (mark))
             (error "Internal error in isearch kill function.")))
         (isearch-exit)))
   #+END_SRC

** Some addtional keybindings
   #+BEGIN_SRC emacs-lisp
     (define-key isearch-mode-map (kbd "C-o") 'isearch-occur)
     (define-key isearch-mode-map "\C-\M-w" 'isearch-yank-symbol)
     (define-key isearch-mode-map (kbd "M-z") 'zap-to-isearch)
   #+END_SRC


* Wgrep allows modifying the grep buffer and apply the changes to the files
** General setting
  #+BEGIN_SRC emacs-lisp
    (setq wgrep-auto-save-buffer t)
    (setq wgrep-enable-key (kbd "C-c C-e"))
  #+END_SRC

** Enable wgrep for ag
   #+BEGIN_SRC emacs-lisp
     (add-hook 'ag-mode-hook 'wgrep-ag-setup)
   #+END_SRC


* Show all lines matching REGEXP in buffers with this major mode.
  Credits: [[http://www.masteringemacs.org/articles/2011/07/20/searching-buffers-occur-mode/][mastering-emacs]]
  #+BEGIN_SRC emacs-lisp
    ;; Get lines matching REGEXP in all buffers with this major mode
    (defun get-buffers-matching-mode (mode)
      "Returns a list of buffers where their major-mode is equal to MODE"
      (let ((buffer-mode-matches '()))
        (dolist (buf (buffer-list))
          (with-current-buffer buf
            (if (eq mode major-mode)
                (add-to-list 'buffer-mode-matches buf))))
        buffer-mode-matches))

    (defun multi-occur-in-this-mode ()
      "Show all lines matching REGEXP in buffers with this major mode."
      (interactive)
      (multi-occur
       (get-buffers-matching-mode major-mode)
       (car (occur-read-primary-args))))
  #+END_SRC


* Replace words in parallel
  Credits: [[http://stackoverflow.com/questions/2588277/how-can-i-swap-or-replace-multiple-strings-in-code-at-the-same-time][stackoverflow]]
  #+BEGIN_SRC emacs-lisp
    (defun parallel-query-replace (plist &optional delimited start end)
      "Replace every occurrence of the (2n)th token of PLIST in
    buffer with the (2n+1)th token; if only two tokens are provided,
    replace them with each other (ie, swap them).

    If optional second argument DELIMITED is nil, match words
    according to syntax-table; otherwise match symbols.

    When called interactively, PLIST is input as space separated
    tokens, and DELIMITED as prefix arg."
      (interactive
       `(,(loop with input = (read-from-minibuffer "Replace: ")
                with limit = (length input)
                for  j = 0 then i
                for (item . i) = (read-from-string input j)
                collect (prin1-to-string item t) until (<= limit i))
         ,current-prefix-arg
         ,@(if (use-region-p) `(,(region-beginning) ,(region-end)))))
      (let* ((alist (cond ((= (length plist) 2) (list plist (reverse plist)))
                          ((loop for (key val . tail) on plist by #'cddr
                                 collect (list (prin1-to-string key t) val)))))
             (matcher (regexp-opt (mapcar #'car alist)
                                  (if delimited 'words 'symbols)))
             (to-spec `(replace-eval-replacement replace-quote
                                                 (cadr (assoc-string (match-string 0) ',alist
                                                                     case-fold-search)))))
        (query-replace-regexp matcher to-spec nil start end)))
  #+END_SRC


* Keybindings for visual-regexp
  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "C-M-%") 'vr/query-replace)
    (global-set-key (kbd "C-S-v") 'vr/mc-mark)
  #+END_SRC


* Configurations for anzu
** Load anzu
   #+BEGIN_SRC emacs-lisp
     (global-anzu-mode +1)
   #+END_SRC

** Sample configuration taken from README
   #+BEGIN_SRC emacs-lisp
     (setq anzu-mode-lighter "")
     (setq anzu-deactivate-region t)
     (setq anzu-search-threshold 1000)
     (setq anzu-replace-to-string-separator " => ")
   #+END_SRC

** Keybindings
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "M-%") 'anzu-query-replace)
   #+END_SRC


* Keep the search results in the center in incremental search
  #+BEGIN_SRC emacs-lisp
    (iqbal-reorient-after-func #'isearch-repeat-forward)
    (iqbal-reorient-after-func #'isearch-repeat-backward)
  #+END_SRC


* Enable highlighting of search term in ag
  #+BEGIN_SRC emacs-lisp
    (setq ag-highlight-search (when (executable-find "ag")
                                (version<= "0.14"
                                           (cl-third (split-string (shell-command-to-string "ag --version"))))))
  #+END_SRC


* Use <tab> to complete word using dabbrev during isearch
  #+BEGIN_SRC emacs-lisp
    (define-key isearch-mode-map (kbd "<tab>") 'isearch-dabbrev-expand)
  #+END_SRC


* Enable global rxt mode
  #+BEGIN_SRC emacs-lisp
    (rxt-global-mode)
  #+END_SRC


* Keybindings
** Keybinding for occur-edit-mode
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-occur-setup-keys ()
       (define-key occur-mode-map (kbd "C-c C-e") #'occur-edit-mode))

     (add-hook 'occur-mode-hook #'iqbal-occur-setup-keys)
   #+END_SRC

** Additional keys on search map
   #+BEGIN_SRC emacs-lisp
     (define-key search-map "o" #'occur)
     (define-key search-map "O" #'multi-occur)
     (define-key search-map "M" #'multi-occur-in-matching-buffers)
     (define-key search-map "s" #'swoop)
     (define-key search-map "S" #'swoop-multi)
     (define-key search-map "n" #'find-name-dired)
     (define-key search-map "g" #'find-grep-dired)
     (define-key search-map "f" #'find-grep)
     (define-key search-map "d" #'lgrep)
     (define-key search-map "r" #'rgrep)
     (define-key search-map "z" #'zrgrep)
     (define-key search-map (kbd "p g") #'projectile-grep)
     (define-key search-map "l" #'locate)
   #+END_SRC

** Keybindings for ag
   #+BEGIN_SRC emacs-lisp
     (when (executable-find "ag")
       (define-key search-map (kbd "p s") #'projectile-ag)
       (define-key search-map (kbd "a s") #'ag)
       (define-key search-map (kbd "a f") #'ag-files)
       (define-key search-map (kbd "a d") #'ag-dired)
       (define-key search-map (kbd "a p") #'ag-project))
   #+END_SRC

** Keybindings for pcre2el
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'pcre2el
       ;; Steal away
       (define-key rxt-mode-map (kbd "C-c /") nil)

       (define-key search-map (kbd "/ /") 'rxt-explain)
       (define-key search-map (kbd "/ c") 'rxt-convert-syntax)
       (define-key search-map (kbd "/ x") 'rxt-convert-to-rx)
       (define-key search-map (kbd "/ '") 'rxt-convert-to-strings)

       ;; From PCRE
       (define-key search-map (kbd "/ p /") 'rxt-explain-pcre)
       (define-key search-map (kbd "/ p e") 'rxt-pcre-to-elisp)
       (define-key search-map (kbd "/ p x") 'rxt-pcre-to-rx)
       (define-key search-map (kbd "/ p '") 'rxt-pcre-to-strings)

       ;; From Elisp
       (define-key search-map (kbd "/ e /") 'rxt-explain-elisp)
       (define-key search-map (kbd "/ e p") 'rxt-elisp-to-pcre)
       (define-key search-map (kbd "/ e x") 'rxt-elisp-to-rx)
       (define-key search-map (kbd "/ e '") 'rxt-elisp-to-strings)
       (define-key search-map (kbd "/ e t") 'rxt-toggle-elisp-rx)
       (define-key search-map (kbd "/ t") 'rxt-toggle-elisp-rx)

       ;; Search
       (define-key search-map (kbd "/ %") 'pcre-query-replace-regexp))
   #+END_SRC
