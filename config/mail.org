* Install the required libraries
** Install mu when on Linux
   #+BEGIN_SRC emacs-lisp
     (when (eq system-type 'gnu/linux)
       ;; Install only if the prerequisites are satisfied
       (if (and (executable-find "autoreconf")
                (executable-find "xapian-config")
                (executable-find "libtool")
                (or (and (getenv "GMIME_CFLAGS")
                         (getenv "GMIME_LIBS"))
                    (when (executable-find "pkg-config")
                      (zerop (shell-command "pkg-config --exists gmime-2.6")))))
           (progn (iqbal-install-packages '(mu4e mu4e-alert))
                  (when (and (boundp 'mu4e-maildir)
                             (not (file-exists-p mu4e-maildir)))
                    (warn "Please set `mu4e-maildir' to your maildir")))
         (message "You need to install `autoconf', `libxapian-dev', `libtool'
     and `libgmime-2.6-dev' for building mu4e")))
   #+END_SRC

** Install notify.el for notifications
   #+BEGIN_SRC emacs-lisp
     (iqbal-install-package 'notify)
   #+END_SRC


* Set mail-host-address
  #+BEGIN_SRC emacs-lisp
    (setq mail-host-address "emacs.localhost")
  #+END_SRC


* Auto-complete addresses in 'From', 'To' field in message mode
** Remove text-properties that interfere with company mode's menu
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-strip-message-mode-problematic-text-props ()
       (save-excursion
         (goto-char (point-min))
         (when (search-forward-regexp (concat "^" mail-header-separator) nil t)
           (remove-text-properties (match-beginning 0) (match-end 0) '(rear-nonsticky)))))

     (add-hook 'message-mode-hook 'iqbal-strip-message-mode-problematic-text-props)
     (add-hook 'mu4e-compose-mode-hook 'iqbal-strip-message-mode-problematic-text-props)
   #+END_SRC


* Enable flyspell mode in message-mode
  #+BEGIN_SRC emacs-lisp
    (add-hook 'message-mode-hook 'flyspell-mode)
    (add-hook 'mu4e-compose-mode-hook 'flyspell-mode)
  #+END_SRC


* Offlineimap configuration
** Use offlineimap to update messages in mu4e
  #+BEGIN_SRC emacs-lisp
    (when (executable-find "offlineimap")
      (setq mu4e-get-mail-command "offlineimap"))
  #+END_SRC

** .offlineimaprc is a unix conf file
  #+BEGIN_SRC emacs-lisp
    (add-to-list 'auto-mode-alist '("\\.offlineimaprc" . conf-mode))
  #+END_SRC


* General mu4e configuration
** Use w3m to render html messages
  #+BEGIN_SRC emacs-lisp
    (when (executable-find "w3m")
      (setq mu4e-view-prefer-html t)
      (setq mu4e-html2text-command "w3m -dump -T text/html"))
  #+END_SRC

** View images inline in message view buffer
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-view-show-images t)

    (when (fboundp 'imagemagick-register-types)
      (imagemagick-register-types))
  #+END_SRC

** Do not insert signature in sent emails
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-compose-signature-auto-include nil)
  #+END_SRC

** It is OK to use non-ascii characters
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-use-fancy-chars t)
  #+END_SRC

** Save attachments in ~/Downloads directory
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-attachment-dir "~/Downloads")
  #+END_SRC

** The information to displayed in the header line
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-headers-fields '((:human-date . 20)
                                (:tags . 12)
                                (:flags . 5)
                                (:from . 22)
                                (:to . 22)
                                (:subject)))
  #+END_SRC

** Make bindings to open links in mu4e consistent with rest of emacs
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'mu4e-view
       (define-key mu4e-view-clickable-urls-keymap iqbal-open-link #'mu4e~view-browse-url-from-binding))
   #+END_SRC

** Always show email addresses in mu4e
   #+BEGIN_SRC emacs-lisp
     (setq mu4e-view-show-addresses t)
   #+END_SRC

** Do not display duplicate messages
   #+BEGIN_SRC emacs-lisp
     (setq mu4e-headers-skip-duplicates t)
   #+END_SRC

** Kill message buffer after email is sent
   #+BEGIN_SRC emacs-lisp
     (setq message-kill-buffer-on-exit t)
   #+END_SRC

** Do not use ido by default
   #+BEGIN_SRC emacs-lisp
     (setq mu4e-completing-read-function 'completing-read)
   #+END_SRC

** Do not confirm on quit
   #+BEGIN_SRC emacs-lisp
     (setq mu4e-confirm-quit nil)
   #+END_SRC


* Apply format=flowed to outgoing messages 
  [[http://www.djcbsoftware.nl/code/mu/mu4e/Writing-messages.html][mu4e manual]] says this should add format=flowed for autogoing messages
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-mu4e-toggle-hard-newlines ()
      (use-hard-newlines nil 'guess))

      ;; Result isn't as good as it sounds
    ;(add-hook 'mu4e-compose-mode-hook #'iqbal-mu4e-toggle-hard-newlines)
  #+END_SRC


* Additional ways to attach files
** Making gnus-dired aware of mu4e
*** Autoload `gnus-dired-attach`
    #+BEGIN_SRC emacs-lisp
      (autoload 'gnus-dired-attach "gnus-dired")
    #+END_SRC

*** Monkey patch `gnus-dired-mail-buffers' to use mu4e buffers to attach files`
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'mu4e
       (defun iqbal-gnus-dired-mail-buffers ()
         "Return a list of active message buffers."
         (let (buffers)
           (save-current-buffer
             (dolist (buffer (buffer-list t))
               (set-buffer buffer)
               (when (and (derived-mode-p 'message-mode)
                          (null message-sent-message-via))
                 (push (buffer-name buffer) buffers))))
           (nreverse buffers)))

       ;; Monkey patch gnus-dired to consider mu4e messages
       (advice-add #'gnus-dired-mail-buffers :override #'iqbal-gnus-dired-mail-buffers)

       (setq gnus-dired-mail-mode 'mu4e-user-agent))
   #+END_SRC

** Attach files from dired 
*** Attaching files in selected region
    #+BEGIN_SRC emacs-lisp
      (defun iqbal-mu4e-file-attach-files-from-region ()
        (let ((start (region-beginning))
              (end (region-end))
              files)
          (save-excursion
            (goto-char start)
            (while (< (point) end)
              (add-to-list 'files (dired-get-file-for-visit))
              (forward-line +1)))
          (gnus-dired-attach files)))
    #+END_SRC

*** Attaching marked files
    #+BEGIN_SRC emacs-lisp
      (eval-when-compile (require 'dired))

      (defun iqbal-mu4e-file-attach-marked-files ()
        (gnus-dired-attach (dired-map-over-marks (dired-get-file-for-visit) nil)))
    #+END_SRC

*** Tying them together
    #+BEGIN_SRC emacs-lisp
      (defun iqbal-mu4e-attach-files-from-dired ()
        (interactive)
        (if (region-active-p)
            (iqbal-mu4e-file-attach-files-from-region)
          (iqbal-mu4e-file-attach-marked-files)))
    #+END_SRC

*** Keybindings
    #+BEGIN_SRC emacs-lisp
      (with-eval-after-load 'dired
        (define-key dired-mode-map (kbd "a") #'iqbal-mu4e-attach-files-from-dired))
    #+END_SRC

** Attach files from helm-file-files
   Credits: https://www.reddit.com/r/emacs/comments/3l3ukg/mu4e_and_helm_attachments/cv33w9b
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'helm-files
       (add-to-list 'helm-find-files-actions
                    '("Attach files for mu4e" . iqbal-helm-mu4e-attach) t)

       (defun iqbal-helm-mu4e-attach (_file)
         (gnus-dired-attach (helm-marked-candidates))))
   #+END_SRC


* Additional actions for messages
** Action to retag message
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'mu4e
       (add-to-list 'mu4e-headers-actions
                    (cons "retag" 'mu4e-action-retag-message)
                    t)
       (add-to-list 'mu4e-view-actions
                    (cons "retag" 'mu4e-action-retag-message)
                    t))
   #+END_SRC

** Action to view current message in a browser
   #+BEGIN_SRC emacs-lisp
     (defun iqbal-mu4e-action-view-in-browser (msg)
       "Modified version of original `mu4e-action-view-in-browser' this adds a meta
     tag to charset, hardcoded to utf8 field, this makes the resulting document
     render properly in browser.

     The code assumes that the message is encoded in UTF-8, since finding the original
     encoding will require parsing the original message and most of the times the
     messages are utf-8 encoded"
       (let* ((html (mu4e-message-field msg :body-html))
              (txt (mu4e-message-field msg :body-txt))
              (tmpfile (format "%s%x.html" temporary-file-directory (random t))))
         (unless (or html txt)
           (mu4e-error "No body part for this message"))
         (with-temp-buffer
           (let* ((msg-text (or html (concat "<pre>" txt "</pre>")))
                  (html-format "<html><head><meta http-equiv=\"Content-Type\" content=\"text/html;charset=UTF-8\"></head>%s</html>"))
             (insert (if (string-prefix-p "<html" msg-text)
                         ;; If the html starts with <html, it probably already
                         ;; has the encoding declared
                         msg-text
                       ;; Otherwise add head with charset
                       (format html-format
                               ;; Wrap the text in body tag, usually not needed
                               ;; since modern browsers handle such malformed content
                               (format "%s%s%s"
                                       (unless (string-prefix-p "<body" msg-text) "<body>")
                                       msg-text
                                       (unless (string-prefix-p "<body" msg-text) "</body>")))))
             (write-file tmpfile)
             (browse-url (concat "file://" tmpfile))))))

     (with-eval-after-load 'mu4e
       (add-to-list 'mu4e-view-actions '("View in browser" . iqbal-mu4e-action-view-in-browser)))
   #+END_SRC

** Action to view current message in w3m
  #+BEGIN_SRC emacs-lisp
    (defun mu4e-action-view-in-w3m (msg)
      "View message in w3m"
      (let ((browse-url-browser-function #'w3m-browse-url))
        (iqbal-mu4e-action-view-in-browser msg)))

    (with-eval-after-load 'mu4e
      (when (locate-library "w3m")
        (add-to-list 'mu4e-view-actions '("open in w3m" . mu4e-action-view-in-w3m))))
  #+END_SRC

** Action to import appointments from ical files
*** Functions to parse ical file
    #+BEGIN_SRC emacs-lisp
      (require 'org-import-icalendar)

      (defun iqbal-parse-ical-event (event)
        ;; org-import-icalendar expects e to be bound
        (let ((e event))
          (list :location (iqbal-cleanup-ical-text (icalendar--get-event-property event 'LOCATION))
                :summary (iqbal-cleanup-ical-text (icalendar--convert-string-for-import
                                                   (or (icalendar--get-event-property event 'SUMMARY)
                                                       "No summary")))
                :description (iqbal-cleanup-ical-text (icalendar--get-event-property event 'DESCRIPTION))
                :date (org-import-icalendar-get-org-timestring event)
                :uid (icalendar--get-event-property event 'UID)
                :attachment (icalendar--get-event-property event 'ATTACH)
                :attendees (icalendar--get-event-properties event 'ATTENDEE)
                :status (icalendar--get-event-property event 'STATUS))))

      (defun iqbal-parse-ical-file (file)
        (with-temp-buffer
          (message (format "Parsing appts from %s" file))
          (insert-file-contents file)
          (iqbal-dos-to-unix)
          (goto-char (point-min))
          (let* ((ical-data (icalendar--read-element nil nil))
                 (zone-map (icalendar--convert-all-timezones ical-data))
                 (events (icalendar--all-events ical-data)))
            (mapcar #'iqbal-parse-ical-event events))))
    #+END_SRC

*** Functions to convert the parsed ical data to appt
    #+BEGIN_SRC emacs-lisp
      (defun iqbal-cleanup-ical-text (text)
        (and (stringp text)
             (replace-regexp-in-string "\\\\," "," (replace-regexp-in-string "\\\\n" "\n" text))))

      (defun iqbal-make-appt-from-parsed-ical-data (data source)
        (with-current-buffer (find-file-noselect (iqbal-get-file-in-data-directory "agenda/appt.org"))
          (goto-char (point-max))
          (newline)
          (delete-blank-lines)
          (insert (format "* TODO %s\n\n%s\n"
                          (plist-get data :summary)
                          (iqbal-indent-text (plist-get data :description) 2)
                          source))
          (org-schedule nil (plist-get data :date))
          (org-entry-put (point) "ID" (plist-get data :uid))
          (org-id-add-location (plist-get data :uid) (buffer-file-name (buffer-base-buffer)))
          ;; Add attachment if present
          (when (and (plist-get data :attachment)
                     (not (string= (plist-get data :attachment) "")))
            (org-entry-put (point)
                           "ATTACHMENT"
                           (plist-get data :attachment)))

          ;; Add location if persent
          (when (and (plist-get data :location)
                     (not (string= (plist-get data :location) "")))
            (org-entry-put (point)
                           "LOCATION"
                           (plist-get data :location)))

          ;; Add attendees if present
          (when (plist-get data :attendees)
            (org-entry-put (point)
                           "ATTENDEES"
                           (string-join (plist-get data :attendees) ", ")))

          (when (plist-get data :status)
            (ignore-errors (org-todo (upcase (plist-get data :status)))))

          (insert (format "\nSource: %s\n" source))))

      (defun iqbal-process-existing-appt (data source)
        (save-window-excursion
          (org-id-goto (plist-get data :uid))
          (org-set-property "ID" (concat (plist-get data :uid) "_archived"))
          (org-archive-subtree)
          (iqbal-make-appt-from-parsed-ical-data data source)))

      (defun iqbal-make-appts-from-parsed-ical-data (data source)
        (dolist (ical-data data)
          (let ((exisiting-appt (org-id-find (plist-get ical-data :uid))))
            (if (not exisiting-appt)
                (iqbal-make-appt-from-parsed-ical-data ical-data source)
              (iqbal-process-existing-appt ical-data source)))))
    #+END_SRC

*** Hooking the above into mu4e
    #+BEGIN_SRC emacs-lisp
      (defun mu4e-action-appt-from-ics (msg)
        (dolist (index (hash-table-keys mu4e~view-attach-map))
          (let* ((attachment (mu4e~view-get-attach msg index))
                 (msgpath (plist-get msg :path))
                 (tmpdir (make-temp-file "mu4e" t))
                 (dest (string-join (list tmpdir (plist-get attachment :name)) "/"))
                 (source-link (org-store-link nil)))
            (when (string= (plist-get attachment :mime-type)
                           "application/ics")
              ;; Calling the server to save does not work reliably specifically
              ;; sometimes the file extracted is blank when Emacs reads it, though
              ;; if read later it has the extracted contents, maybe file system
              ;; cache issue, who knows! :confused:
              (shell-command (format "%s extract %s --parts=%d --target-dir=%s"
                                     mu4e-mu-binary
                                     (shell-quote-argument msgpath)
                                     (plist-get attachment :index)
                                     tmpdir))
              (iqbal-make-appts-from-parsed-ical-data (iqbal-parse-ical-file dest) source-link)
              (message (format "Imported %s" (plist-get attachment :name)))))))

      (with-eval-after-load 'mu4e
        (add-to-list 'mu4e-view-actions (cons "ical to appt" 'mu4e-action-appt-from-ics) t))
    #+END_SRC


* Auto update configuration
  #+BEGIN_SRC emacs-lisp
    (setq mu4e-hide-index-messages t)
    (setq mu4e-get-mail-command "offlineimap")
    (setq mu4e-update-interval 300)
  #+END_SRC


* Additional bookmarks for mu4e
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-get-all-mail-query ()
      (require 'subr-x)
      (string-join (mapcar (lambda (maildir) (format "maildir:\"%s\"" maildir))
                           (cl-remove-if
                            (lambda (maildir) (or (string-match-p "[Ss]pam" maildir) (string-match-p "[Tt]rash" maildir)))
                            (cl-set-difference (mu4e-get-maildirs) (list mu4e-trash-folder) :test #'string=)))
                   " OR "))

    (with-eval-after-load 'mu4e
      (add-to-list 'mu4e-bookmarks `(,(iqbal-get-all-mail-query) "All mail" ?a))
      (add-to-list 'mu4e-bookmarks '("flag:attach" "Mails with attachement" ?T) t))
  #+END_SRC


* Start mu4e
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-start-mu4e-bg ()
      "Start in background avoiding any prompts and ignoring errors"
      (when (and (require 'mu4e nil t)
                 (file-directory-p mu4e-maildir)
                 (file-directory-p (concat mu4e-maildir mu4e-sent-folder))
                 (file-directory-p (concat mu4e-maildir mu4e-drafts-folder))
                 (file-directory-p (concat mu4e-maildir mu4e-trash-folder)))
        (ignore-errors (mu4e t)
                       (setq mail-user-agent 'mu4e-user-agent))))

    (add-hook 'after-init-hook #'iqbal-start-mu4e-bg)
  #+END_SRC


* mu4e-alert configuration
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-enable-mu4e-alert ()
      (when (locate-library "mu4e-alert")
        (mu4e-alert-set-default-style (if (executable-find "notify-send")
                                          'libnotify
                                        'notifications))
        (mu4e-alert-enable-notifications)
        (mu4e-alert-enable-mode-line-display)))

    (add-hook 'after-init-hook #'iqbal-enable-mu4e-alert)
  #+END_SRC


* Configuration for sending mail
** Sending mail from multiple smtp accounts when using mu4e
  #+BEGIN_SRC emacs-lisp
    (defvar iqbal-mu4e-account-alist nil "List of accounts in format specified here [http://www.djcbsoftware.nl/code/mu/mu4e/Multiple-accounts.html]")

    (defun iqbal-mu4e-set-account ()
      "Set the account for composing a message."
      (let* ((account
              ;; If we are about to compose a reply retrieve try retrieving the
              ;; the account corresponding to 'to' field of email
              (if mu4e-compose-parent-message
                  (let ((receiving-email (cdar (mu4e-message-field mu4e-compose-parent-message
                                                                   :to))))
                    (caar (cl-remove-if-not (lambda (account)
                                           (string= (cadr (assoc 'user-mail-address account))
                                                    receiving-email))
                                         iqbal-mu4e-account-alist)))
                ;; Otherwise read the account to use from the user
                (when iqbal-mu4e-account-alist
                  (completing-read (format "Compose with account: (%s) "
                                           (mapconcat #'(lambda (var) (car var))
                                                      iqbal-mu4e-account-alist "/"))
                                   (mapcar #'(lambda (var) (car var)) iqbal-mu4e-account-alist)
                                   nil t nil nil (caar iqbal-mu4e-account-alist)))))
             ;; Retrieve the variables corresponding to account
             (account-vars (cdr (assoc account iqbal-mu4e-account-alist))))
        (when account-vars
          ;; Set the variables
          (mapc #'(lambda (var)
                    (set (car var) (cadr var)))
                account-vars))))

    (add-hook 'mu4e-compose-pre-hook 'iqbal-mu4e-set-account)
  #+END_SRC

** Prefer .authinfo.gpg for credentials
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'auth-source
       (setq auth-sources (cons "~/.authinfo.gpg"
                                (delete "~/.authinfo.gpg" auth-sources))))
   #+END_SRC

** Send mail using smtp
   #+BEGIN_SRC emacs-lisp
     (setq send-mail-function 'smtpmail-send-it)
   #+END_SRC


* Integration with org-mode
** Register a handler to open links to mu4e messages
  #+BEGIN_SRC emacs-lisp
    (when (locate-library "org-mu4e")
      (autoload 'org-mu4e-open "org-mu4e")
      (with-eval-after-load 'org
        (org-add-link-type "mu4e" 'org-mu4e-open)))
  #+END_SRC

** Load org-mu4e on loading mu4e
   #+BEGIN_SRC emacs-lisp
     (with-eval-after-load 'mu4e (require 'org-mu4e nil t))
   #+END_SRC


* Advice mu4e~proc-sentinel so that path to mu binary is copied to clipboard
  This is needed since in case mu is installed using el-get, which is buried
  deep in .emacs.d folder and might not be in PATH, as such it cannot be run
  directly from shell. The following advice copies the path to mu to clipboard,
  so that it can be directly run from shell
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-advise-mu4e~proc-sentinel (orig &rest args)
      (condition-case err
          (apply orig args)
        (error (progn (kill-new mu4e-mu-binary)
                      (error "Failed to start mu. %s. Path to mu binary (%s) copied to clipboard."
                             (error-message-string err)
                             mu4e-mu-binary)))))

    (advice-add 'mu4e~proc-sentinel :around #'iqbal-advise-mu4e~proc-sentinel)
  #+END_SRC


* Advice mu4e-view-quit to switch to main buffer no header buffer
  #+BEGIN_SRC emacs-lisp
    (defun iqbal-mu4e-switch-to-main-buffer-maybe (&rest ignored)
      (unless (eq (current-buffer) mu4e~headers-buffer)
        (when (get-buffer mu4e~main-buffer-name)
          (switch-to-buffer mu4e~main-buffer-name))))

    (advice-add 'mu4e~view-quit-buffer :after #'iqbal-mu4e-switch-to-main-buffer-maybe)
  #+END_SRC


* Keybindings mu4e
  #+BEGIN_SRC emacs-lisp
    (define-key iqbal-launcher-map "m" #'mu4e)

    (with-eval-after-load 'mu4e
      (define-key mu4e-view-mode-map (kbd "U") #'mu4e-headers-rerun-search)
      (define-key mu4e-main-mode-map (kbd "q") #'bury-buffer)
      (define-key mu4e-main-mode-map (kbd "Q") #'mu4e-quit))
  #+END_SRC
